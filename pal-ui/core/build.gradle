/*
 * Copyright 2016 SRI International
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// $Id: build.gradle 7401 2016-03-25 20:18:20Z Chris Jones (E24486) $
description = "Core functionality for the FX-based UI"

apply plugin: 'SriJava'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            source sourceSets.main.java
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            source sourceSets.test.java
        }
    }
}

archivesBaseName = "pal-ui-core"

// This horrible thing is necessary to exclude the jfxrt dependency
// from making it into the Maven POM file. Because there's no version
// info attached to jfxrt.jar, it causes Maven to reject the POM.
uploadArchives.repositories.find { repo ->
    repo instanceof MavenDeployer
}.each { deployer ->
    deployer.pom.whenConfigured { pom ->
        pom.dependencies.find { dep ->
            dep.artifactId == 'jfxrt'
        }.each { dep ->
            pom.dependencies.remove(dep)
        }
    }
}

dependencies {
    compile project(':bridge')
    testCompile project(path: ':common', configuration: 'testCompile')
}
try {
    Class.forName("javafx.stage.Stage")
} catch(Exception e) {
    dependencies {
        compile 'com.sun:jfxrt:'
    }
}
